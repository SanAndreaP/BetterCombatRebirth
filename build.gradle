buildscript {
    repositories {
        jcenter()
        maven {
            name = "forge"
            url = "http://files.minecraftforge.net/maven"
        }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT'
    }
}
apply plugin: 'net.minecraftforge.gradle.forge'

apply plugin: 'idea'
idea {
    module {
        inheritOutputDirs = true
    }
    project {
        languageLevel = '1.8' //due to a bug in gradle or IDEA, this doesn't work :(
    }
}

version = "1.12.2-1.5.6"
group = "bettercombat.mod"
archivesBaseName = "BetterCombat"

sourceCompatibility = targetCompatibility = "1.8" // Need this here so eclipse task generates correctly.
compileJava {
    sourceCompatibility = targetCompatibility = "1.8"
}

minecraft {
    version = "1.12.2-14.23.5.2768"
    runDir = "run"
    mappings = "snapshot_20180301"
}

repositories {
    mavenCentral()
    maven {
        url "http://dvs1.progwml6.com/files/maven"
    }
    maven {
        name = "CurseForge"
        url = "https://minecraft.curseforge.com/api/maven/"
    }
}

dependencies {
    deobfCompile "aether-legacy:aether_legacy:1.12.2:v3.2"
    deobfCompile "slimeknights.mantle:Mantle:1.12-1.3.3.41"
    deobfCompile("slimeknights:TConstruct:1.12.2-2.9.1.70") {
        transitive = false
    }
}

processResources {
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'
        
        expand 'version':project.version, 'mcversion':project.minecraft.version
    }

    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }

    rename '(.+_at.cfg)', 'META-INF/$1'
}

jar {
    manifest {
        attributes 'FMLAT': 'bettercombat_at.cfg'
    }
}

artifacts {
    archives jar
}
